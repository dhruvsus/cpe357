#!/bin/tcsh -f 

if ( `hostname` == "tahoe.csc.calpoly.edu" ) then
  set lib=/home/pnico/CalPoly/Class/cpeX317/w04/Asgn/Handin/lib/Asgn2
  set logprog=echo 
  set longlines=/home/pnico/bin/longlines.pl
  set reference=/home/pnico/CalPoly/Class/cpeX317/w04/Asgn/asgn2/Soln/fw
  set bindir=/home/pnico/bin
else if ( `hostname` == "lagniappe.csc.calpoly.edu" ) then
  set lib=/home/pnico/CalPoly/Class/cpeX317/w04/Asgn/Handin/lib/Asgn2
  set logprog=echo 
  set longlines=/home/pnico/bin/longlines.pl
  set reference=/home/pnico/CalPoly/Class/cpeX317/w04/Asgn/scratch/WordCount/fw
  set bindir=/home/pnico/bin
else
  set lib=/home/pn-cs357/lib/asgn2/
  set logprog=/home/pn-cs357/bin/logit.asgn2
  set longlines=/home/pnico/bin/longlines.pl
  set reference=/home/pn-cs357/demos/fw
  set bindir=/home/pn-cs357/bin
  set iolimit=/home/pnico/bin/iolimit 
  set timelimit=/home/pnico/bin/timelimit
endif

# add bindir to the head of the path
set path = ( /home/pnico/bin $bindir $path )

set testdir=$lib/Tests
set exitstatus = 0

set diffsfile=diffs
set diffout=diffsout

set limitlines=150
set limitchars=8192
#set timeout=120   # timeout is determined below

if ( -f $diffsfile ) then
  rm $diffsfile 
endif

onintr bail

if ( $exitstatus == 0 ) then
  echo ""
  echo "------------------------"
  echo "Looking for source files"
  echo "------------------------"
  echo ""
  set nonomatch
  if ( *.[ch] != '*.[ch]' ) then
    set src = ( *.[ch] )
    echo "Found: $src"
  else
    echo "***** No C source files found.  "
    echo "***** Are you sure you're in the right directory?"
    echo ""
    set exitstatus = -1
  endif
endif

if ( $exitstatus == 0 ) then
  echo ""
  echo "-----------------------"
  echo "Looking for a Makefile"
  echo "----------------------"
  echo ""

  if ( $exitstatus == 0 ) then
    if ( -f Makefile ) then
      echo "Found: Makefile"
    else if ( -f makefile ) then 
      echo "Found: makefile"
    else 
      echo ""
      echo "***** No Makefile found."
      echo ""
      set exitstatus = -2
    endif
  endif
endif

if ( $exitstatus == 0 ) then
  echo ""
  echo "--------------------------------"
  echo "Checking for long lines (no program should have any lines > 80 chars)"
  echo "--------------------------------"
  echo ""
  if ( ! { $longlines $src } ) then
     set exitstatus=-3
  else
     echo "Line length ok."
  endif
  echo ""
endif

if ( $exitstatus == 0 ) then
  echo ""
  echo "------------------------------------"
  echo "Building the application:" \"make fw\"
  echo "------------------------------------"
  echo ""

  set cmd = ( make fw )
  echo "$cmd" 
  if ( ! { $cmd  } ) then
    echo ""
    echo "Unable to compile fw."
    set exitstatus = -4
  endif
endif

if ( $exitstatus == 0 ) then
  echo ""
  echo "---------------"
  echo "checking for fw"
  echo "---------------"
  echo ""

  if ( ! -f fw ) then
    echo "program fw not found"
    set exitstatus = -5
  endif
endif

if ( $exitstatus == 0 ) then
  echo ""
  echo "------------------------"
  echo "Running the application "
  echo "------------------------"
  echo ""

  echo ""
  echo "--------------------------------------------------------"
  echo "Test Results:"
  echo "   These are the results of trying your fw against"   
  echo "mine on a number of different test cases. "
  echo ""
  echo "  The output of your version of fw is compared to "
  echo "the output of ~pn-cs357/demos/fw using diff (1)."
  echo "If they differ, even in whitespace, the test case is a failure."
  echo ""
  echo "  The test commands are located in $testdir/Cmds and the"
  echo "associated data are in $testdir/Data"
  echo ""
  echo "  Note, the first two tests," \"00_usage\" and  \"01_usage\"
  echo "give bad arguments and expect to see a message of the form"
  echo \""usage: fw [-n num] [ file1 [ file2 [...] ] ]"\" "The message"
  echo "doesn't have to be exactly the same, but it's looking for the"
  echo "word "\"usage\".
  echo ""
  echo "  Each command (with one exception) is run with a timeout of "
  echo "2 minutes, so if your program takes more than 2 minutes, it "
  echo "will be interrupted. "
  echo ""
  echo "  The exception is "\"man1\"" which gets 5 minutes."
    
  echo ""
  echo "Tests"
  foreach cmd ( $testdir/Cmds/*[^~] ) 
#  foreach cmd ( $testdir/Cmds/02*[^~] ) 
#  foreach cmd ( $testdir/Cmds/09*[^~] ) 
     set testname=$cmd:t:r
     echo -n "        "$testname"..."               
     
     if ( $testname == "16_man1" ) then
	set timeout=300
     else
	set timeout=120
     endif

     # run each, disposing of stderr
     ( $cmd $reference $testdir/Data > reference.output$$ ) >& /dev/null
     ( $timelimit $timeout $cmd ./fw $testdir/Data > fw.output$$ ) >& /dev/null
     diff  reference.output$$ fw.output$$   >& $diffout
     if ( ! $status ) then 
         echo "ok."                                
     else                                                  
         echo "FAILURE."                           
     	 echo "" >> $diffsfile
     	 echo "==========================================" >> $diffsfile
     	 echo "Test failed: "$testname                        >> $diffsfile
     	 echo "==========================================" >> $diffsfile
     	 echo "  ----------" >> $diffsfile
     	 echo "  Reference:" >> $diffsfile
     	 echo "  ----------" >> $diffsfile
     	 sed 's/^/  /g' reference.output$$ | $iolimit $limitlines $limitchars |& cat -v >> $diffsfile
     	 echo "  ----------" >> $diffsfile
     	 echo "  Submitted:" >> $diffsfile
     	 echo "  ----------" >> $diffsfile
     	 sed 's/^/  /g' fw.output$$ | $iolimit $limitlines $limitchars |& cat -v >> $diffsfile
     	 echo "  ------------------------------" >> $diffsfile
     	 echo "  Diffs (<reference >submitted):" >> $diffsfile
     	 echo "  ------------------------------" >> $diffsfile
     	 sed 's/^/  /g' $diffout | cat -v >> $diffsfile
         @ exitstatus++;
     endif
  end

  echo ""
  echo "--------------------------------------------------------"
  echo ""
endif

endgame:

  if ( ! $exitstatus ) then
    echo "Passed all tests."
  else
    echo "FAILED."
      echo ""
      echo ""
    if ( -f $diffsfile ) then
      echo "#############################"
      echo "# Failure details are below #"
      echo "#############################"
      echo ""
      cat $diffsfile
    endif
  endif

  $logprog  $exitstatus

  rm -f fw.output$$ reference.output$$ $diffsfile diffout
  exit $exitstatus;

bail:
    set exitstatus=6
    echo ""
    echo "Interrupted"
    echo ""
    goto endgame



